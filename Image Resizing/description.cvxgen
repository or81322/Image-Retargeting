# Produced by CVXGEN, 2014-08-13 12:16:12 -0400.
# CVXGEN is Copyright (C) 2006-2012 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2012 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  gridNumOfRows = 25 # M
  gridNumOfCols = 25 # N
end

parameters
  Q (gridNumOfRows + gridNumOfCols, gridNumOfRows + gridNumOfCols) psd
  b (gridNumOfRows + gridNumOfCols)
  targetHeight nonnegative
  targetWidth nonnegative
  imageHeight nonnegative
  imageWidth nonnegative
end

comment
  percentage = 0.2
  minGridHeight = percentage * imageHeight/gridNumOfRows # Lh
  minGridWidth = percentage * imageWidth/gridNumOfCols # Lw
end

variables
  s (gridNumOfRows + gridNumOfCols) nonnegative
  sRows (gridNumOfRows) nonnegative
  sCols (gridNumOfCols) nonnegative
end

minimize
  quad(s, Q) + s' * b

subject to
  # s = [sRows; sCols]
  sRows[i] == s[i], i=1..gridNumOfRows
  sCols[i] == s[i + gridNumOfRows], i=1..gridNumOfCols

  # sRows >= minGridHeight
  sRows >= 0.2 * imageHeight/gridNumOfRows
  # sCols >= minGridWidth
  sCols >= 0.2 * imageWidth/gridNumOfCols

  sum(sRows) == targetHeight
  sum(sCols) == targetWidth
end
